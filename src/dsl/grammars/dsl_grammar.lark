?start: _NL? game? _NL?

game: "GAME" (start_g end_g | end_g start_g) property* (room | player_class | npc)*

start_g: "START" phrase
end_g: "END" phrase

// For room
room: _NL "ROOM" id (property | connections)* (item)*

// For item
item: _NL "ITEM" id "IN" location property* action*
    | _NL "ITEM" id property* action*

// For player class
player_class: _NL "PLAYER_CLASS" id (property | attributes | base_stats | effects | skill_tree | starting_skills)*

attributes: _NL "ATTRIBUTES" attribute_state*
attribute_state: _NL phrase phrase

base_stats: _NL "BASESTATS" stat_state*
stat_state: _NL phrase stat*
stat: _NL phrase NUMBER _NL phrase NUMBER

effects: _NL "effects:" phrase

skill_tree: _NL "skill_tree:" phrase

starting_skills: _NL "starting_skills:" phrase

// actions for items
action: _NL ("action:"|"actions:") action_ids property*
action_ids: id ("," id)*

property: _NL phrase "FOR" id ":" ESCAPED_STRING -> misplaced_property
        | _NL phrase ":" ESCAPED_STRING

// connections for rooms
connections.5: _NL "connections:" connection ((","|_NL) connection)*
connection: phrase "TO" phrase

// For NPC
npc: _NL "NPC" id "IN" location property* inventory dialogue
    | _NL "NPC" id  property* inventory dialogue
    | _NL "NPC" id "IN" location property* inventory
    | _NL "NPC" id "IN" location property*

inventory: _NL "INVENTORY" property*


dialogue: _NL "DIALOGUE" node*
node: _NL "NODE" id edge* property

edge: _NL "EDGE" id "TO" id property

id: phrase

location: phrase

phrase: (STRING+ | NUMBER)


// NEWLINE(s), whitespace, and comments
_NL: ( /(\r?\n[\t ]*)/ | COMMENT) +

%import .util.STRING
%import .util.ESCAPED_STRING

%import common.SIGNED_NUMBER -> NUMBER

%import .util.COMMENT
%ignore COMMENT

%import common.WS_INLINE
%ignore WS_INLINE
