?start: _NL? game? _NL?

game: "GAME" (start_g end_g | end_g start_g) property* (room | class)*


start_g: "START" phrase
end_g: "END" phrase

room: _NL "ROOM" id (property | connections)* item*

item: _NL "ITEM" id "IN" location property* action*
    | _NL "ITEM" id property* action*


class: _NL "PLAYER_CLASS" id property* attribute* base_stats*

attribute: _NL "attributes:" attribute_ids*
attribute_ids: _NL id "set" bool

base_stats: _NL "base stats:" stat_setting*
stat_setting: _NL id _NL "CURRENT" NUMBER _NL "MAX" NUMBER

action: _NL ("action:"|"actions:") action_ids property*
action_ids: id ("," id)*

property: _NL phrase "FOR" id ":" ESCAPED_STRING -> misplaced_property
        | _NL phrase ":" ESCAPED_STRING

connections.5: _NL "connections:" connection ((","|_NL) connection)*
connection: phrase "TO" phrase

npc: _NL "NPC" id "IN" location property* item* dialogue

dialogue: _NL "DIALOGUE" node*
node: "NODE" id edge* property
edge: "EDGE" id "TO" id property

id: phrase

location: phrase

phrase: STRING+

bool: ("TRUE"|"FALSE")


// NEWLINE(s), whitespace, and comments
_NL: ( /(\r?\n[\t ]*)/ | COMMENT) +

%import .util.STRING
%import .util.ESCAPED_STRING

%import common.SIGNED_NUMBER -> NUMBER

%import .util.COMMENT
%ignore COMMENT

%import common.WS_INLINE
%ignore WS_INLINE
